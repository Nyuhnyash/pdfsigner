plugins {
    id 'java'
}

group 'ru.samgtu.pdfsigner'
version '1.0'

repositories {
    mavenCentral()
    maven { url 'https://repo.itextsupport.com/releases' }
    ivy {
        url 'https://github.com/'
        patternLayout {
            artifact '[organisation]/releases/download/[revision]/[artifact](-[revision])(-[classifier]).[ext]'
        }

        // This is required in Gradle 6.0+ as metadata file (ivy.xml) is mandatory.
        metadataSources { artifact() }
    }
}

dependencies {
    implementation 'info.picocli:picocli:4.7.0'
    implementation 'org.slf4j:slf4j-nop:2.0.5'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.72'
    implementation 'com.itextpdf:forms:7.2.5'
    implementation 'com.itextpdf:pdfa:7.2.5'

    // https://github.com/Nyuhnyash/itext7-gost-sign/releases/download/7.2.5/itext7-sign-7.2.5.jar
    implementation 'Nyuhnyash/itext7-gost-sign:itext7-sign:7.2.5'

    annotationProcessor 'com.github.bsideup.jabel:jabel-javac-plugin:1.0.0'
    compileOnly 'com.github.bsideup.jabel:jabel-javac-plugin:1.0.0'
}


configure([tasks.compileJava]) {
    sourceCompatibility = 10 // for the IDE support
    options.release = 8
}



tasks.register('fatJar', Jar) {
    manifest.attributes(
            'Main-Class': project.group + '.Main'
    )

    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    with jar
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // При упаковке Bouncy Castle в архив, цифровую подпись библиотеки больше нельзя не проверить
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}
